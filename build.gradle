/*
 * Copyright (c) 2016, nwillc@gmail.com
 *
 * Permission to use, copy, modify, and/or distribute this software for any
 * purpose with or without fee is hereby granted, provided that the above
 * copyright notice and this permission notice appear in all copies.
 *
 * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
 * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
 * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
 * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 */

buildscript {
    repositories {
        mavenLocal()
        maven {
            url mvn_repository
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'gradle.plugin.com.github.nwillc:vplugin:2.1.1',
                'org.ajoberstar:gradle-git:1.7.2',
                'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0',
                'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.3.0',
                'org.ajoberstar:gradle-git-publish:0.4.1'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'pmd'
apply plugin: 'com.github.nwillc.vplugin'
apply plugin: 'org.ajoberstar.git-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: "info.solidsoft.pitest"

repositories {
    mavenLocal()
    maven {
        url mvn_repository
    }
}

dependencies {
    compile "com.github.nwillc:almost-functional:$almost_functional_version",
            "org.tinylog:tinylog:$tinylog_version"

    testCompile "junit:junit:$junit_version",
            "org.assertj:assertj-core:$assertj_version",
            "org.jmockit:jmockit:$jmockit_version",
            "org.zapodot:embedded-db-junit:$embedded_db_junit_version"

    testCompile("com.github.nwillc:jdk_contract_tests:$jdk_contract_version") {
        exclude module: "junit-jupiter-api"
    }
}

group = 'com.github.nwillc'
archivesBaseName = 'fun-jdbc'
version = '1.0.1-SNAPSHOT'
targetCompatibility = '1.8'
sourceCompatibility = '1.8'

compileJava {
    // options.compilerArgs << '-Xlint:deprecation'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

test {
    jvmArgs = ['-Djdk.attach.allowAttachSelf', '-Dtinylog.level=debug']
    
    beforeTest { descriptor ->
        logger.lifecycle("\tRunning $descriptor.className.$descriptor.name")
    }

    afterSuite { descriptor, result ->
        if (descriptor.parent == null) {
            logger.lifecycle("\tTests run: $result.testCount, Failures: $result.failedTestCount, Skipped: $result.skippedTestCount")
        }
    }

    testLogging {
        showStandardStreams true
        exceptionFormat "full"
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')

    dryRun = false
    publish = true
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = project.name
        desc = 'Java 8 functional JDBC utility code, applying some of Java\'s newer features to reduce JDBC boilerplate code.'
        websiteUrl = 'https://github.com/nwillc/fun-jdbc'
        issueTrackerUrl = 'https://github.com/nwillc/fun-jdbc/issues'
        vcsUrl = 'https://github.com/nwillc/fun-jdbc.git'
        licenses = ['ISC']
        labels = ['jdk8', 'jdbc']
        publicDownloadNumbers = true
    }
}

bintrayUpload.onlyIf { !project.version.toString().endsWith('SNAPSHOT') }

pitest {
    threads = 4
    outputFormats = ['XML', 'HTML']
    pitestVersion = '1.3.2'
}

gitPublish {
    repoUri = 'git@github.com:nwillc/fun-jdbc.git'
    branch = 'gh-pages'
    repoDir = file("$buildDir/somewhereelse")
    contents {
        from 'src/main/pages'
        from(javadoc) {
            into 'javadoc'
        }
    }
}
